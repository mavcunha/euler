#!/usr/bin/env zsh

zparseopts -D -E - e:=ex l:=lang a=all -all=all

typeset exercise=${ex[-1]}
typeset language=${lang[-1]}

typeset -A interpreters
typeset -A extensions

function setlang {
  readonly l=${1:?"Language required"}
  readonly i=${2:-${l}}
  readonly e=${3:-${l}}

  interpreters[${l}]=${i}
  extensions[${l}]=${e}
}

function path {
  readonly lang=${1:?"Language must be defined."}
  readonly ex=${2:?"Exercise number must be defined."}

  echo "src/${ex}/${lang}/${ex}.${extensions[${lang}]}"
}

function run {
  readonly lang=${1:?"Language must be defined."}
  readonly ex=${2:?"Exercise number must be defined."}

  print -P "lang: %F{green}$lang%f"
  file=$(path ${lang} ${ex})
  if [[ -f ${file} ]]; then
    eval ${interpreters[${lang}]} ${file}
  else
    echo "${file} not found."
  fi
}

setlang "wolfram" "wolframscript -file" "wls"
setlang "clojure" "clj -M" "clj"
setlang "python" "python3" "py"
setlang "ruby" "ruby" "rb"
setlang "zsh"
setlang "raku"
setlang "java"
setlang "lua"

if [[ -n ${all} ]]; then
  for l in ${(k)interpreters}; do
    run ${l} ${exercise}
  done
else
  run ${language} ${exercise}
fi
