#!/usr/bin/env zsh

zparseopts -D -E - e:=ex l:=lang -show=show -summary=sum \
  -list=list h=help -help=help -time=timeit -create=createdirs \
  -problem=problem

readonly SOLDIR=${PROJECT_EULER_DIR:-"src"}

typeset exercise=${ex[-1]%.*}
typeset version=${ex[-1]/${exercise}/}
typeset language=${lang[-1]}

typeset -A interpreters
typeset -A extensions

function help {
 cat <<- EOH
euler [-e EXERCISE] [-l LANG] [--show] [--summary] [--list] [--time]
      [--create] [--problem] [-h|--help]

Where EXERCISE is the exercise you want to run, example 001, 002, 003...
and LANG is the language you want it to run. If LANG is absent all
configured languages will run.

-e EXERCISE   The exercise such as 001, 002, 003, ... in fact any
              subdir of the solutions will work.

-l LANG       Narrows to the LANG, where LANG is one of the languages
              configured in 'setlang', example: python, ruby, ...

--show        Instead of executing the exercise it will show its code. If
              EXERCISE is not given then will show for all languages.

--summary     Will show a quick summary of the exercises already found.

--list        Lists the current languages and interpreters configuration.

--time        Will add 'time' to the header of the interpreter for timing
              its execution.

--create      It will create the directories for exercise given by '-e'

--problem     Will show the problem description for '-e'. It actually
              prints out the 'README.md' file on the exercise directory.

-h|--help     Shows this message.

EOH
}

function setlang {
  readonly l=${1:?"Language required"}
  readonly i=${2:-${l}}
  readonly e=${3:-${l}}

  interpreters[${l}]=${i}
  extensions[${l}]=${e}
}

function path {
  readonly l=${1:?"Language required"}
  readonly e=${2:?"Exercise required"}
  readonly v=${3:-""}

  echo "${SOLDIR}/${e}/${l}/${e}${v}.${extensions[${l}]}"
}

function run {
  readonly l=${1:?"Language required"}
  readonly e=${2:?"Exercise required"}
  readonly v=${3:-""}

  print -P "%F{green}${l}%f:"
  file=$(path ${l} ${e} ${v})
  if [[ -f ${file} ]]; then
    eval ${timeit/--/} ${interpreters[${l}]} ${file}
  else
    print -P "%F{001}${e} not found%f"
  fi
}

function show {
  readonly l=${1:?"Language required"}
  readonly e=${2:?"Exercise required"}
  readonly v=${3:-""}

  print -P "%F{green}${l}%f:"
  cat $(path ${l} ${e} ${v})
  return 0
}

function run_or_show {
  readonly l=${1:?"Language required"}
  readonly e=${2:?"Exercise required"}
  readonly v=${3:-""}

  [[ -n ${show} ]] && show ${l} ${e} ${v} || run ${l} ${e} ${v}
}

function main {
  if [[ -z ${language} ]]; then
    for lang in ${(k)interpreters}; do
      run_or_show ${lang} ${exercise} ${version}
    done
  else
    run_or_show ${language} ${exercise} ${version}
  fi
}

function summary {
  print "Project Euler Solution Summary"
  print "using dir: ${SOLDIR}"
  for lang in ${(k)interpreters}; do
    local ext=${extensions[${lang}]}
    local files=($(find ${SOLDIR} -type f -regex ".*\/${lang}\/[0-9]*\.${ext}$" | sort ))

    local r=""
    for file (${files[*]}) r+="${file:t:r} "

    printf "%-18s %s\n" $(print -P  "%F{green}${lang}%f:") "${r}"
  done
}

function list_languages {
  print "Languages defined"
  print -P  "%F{green}name%f -> %F{yellow}interpreter%f -> %F{red}extension%f\n"
  for lang in ${(k)interpreters}; do
    print -P  "%F{green}${lang}%f -> %F{yellow}${interpreters[$lang]}%f -> %F{red}$extensions[$lang]%f"
  done
}

function create_dirs {
  readonly ex=${1:?"Exercise required, use -e"}
  print -P "Creating dirs for exercise: %F{green}${ex}%f"
  for lang in ${(k)interpreters}; do
    print -P  "%F{green}${ex}%f/%F{yellow}${lang}%f"
    mkdir -p ${SOLDIR}/${ex}/${lang}
  done
}

function show_problem {
  readonly ex=${1:?"Exercise required, use -e"}
  print -P "%F{green}${ex}%f: README.md"
  cat ${SOLDIR}/${ex}/README.md
}

setlang "wolfram" "wolframscript -file" "wls"
setlang "clojure" "clj -M" "clj"
setlang "python" "python3" "py"
setlang "sql" "sqlite3 :memory: <" "sql"
setlang "ruby" "ruby" "rb"
setlang "zsh"
setlang "raku"
setlang "java"
setlang "lua"

if [[ -n ${help} ]]; then
  help
elif [[ -n ${sum} ]]; then
  summary
elif [[ -n ${list} ]]; then
  list_languages
elif [[ -n ${createdirs} ]]; then
  create_dirs ${exercise}
elif [[ -n ${problem} ]]; then
  show_problem ${exercise}
else
  main
fi
