#!/usr/bin/env zsh

zparseopts -D -E - e:=ex l:=lang a=all -all=all -show=show s=show

typeset exercise=${ex[-1]%.*}
typeset version=${ex[-1]/${exercise}/}
typeset language=${lang[-1]}

typeset -A interpreters
typeset -A extensions

function setlang {
  readonly l=${1:?"Language required"}
  readonly i=${2:-${l}}
  readonly e=${3:-${l}}

  interpreters[${l}]=${i}
  extensions[${l}]=${e}
}

function path {
  readonly l=${1:?"Language required"}
  readonly e=${2:?"Exercise required"}
  readonly v=${3:-""}

  echo "src/${e}/${l}/${e}${v}.${extensions[${l}]}"
}

function run {
  readonly l=${1:?"Language required"}
  readonly e=${2:?"Exercise required"}
  readonly v=${3:-""}

  print -P "lang: %F{green}${l}%f"
  file=$(path ${l} ${e} ${v})
  if [[ -f ${file} ]]; then
    eval ${interpreters[${l}]} ${file}
  else
    echo "${file} not found."
  fi
}

function show {
  readonly l=${1:?"Language required"}
  readonly e=${2:?"Exercise required"}
  readonly v=${3:-""}

  print -P "lang: %F{green}${l}%f"
  cat $(path ${l} ${e} ${v})
}

function run_or_show {
  readonly l=${1:?"Language required"}
  readonly e=${2:?"Exercise required"}
  readonly v=${3:-""}

  [[ -n ${show} ]] && show ${l} ${e} ${v} || run ${l} ${e} ${v}
}

function main {
  if [[ -n ${all} ]]; then
    for lang in ${(k)interpreters}; do
      run_or_show ${lang} ${exercise} ${version}
    done
  else
    run_or_show ${language} ${exercise} ${version}
  fi
}

setlang "wolfram" "wolframscript -file" "wls"
setlang "clojure" "clj -M" "clj"
setlang "python" "python3" "py"
setlang "ruby" "ruby" "rb"
setlang "zsh"
setlang "raku"
setlang "java"
setlang "lua"

main
